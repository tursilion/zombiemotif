
100 OPTION BASE 1 :: DIM LN(4),LC(4),KA$(5),QX(4),QY(4),MS$(19),SE$(4):: FM=1 :: FX=3 :: FY=7 :: FS=16 :: QD=0 :: AC=0 :: MN=0 :: NL=0 :: NC=0 :: SN=1 :: YR=1900
110 SX=0 :: SY=0 :: SC=0 :: M$="" :: FOR I=1 TO 5 :: READ KA$(I):: NEXT I :: FOR I=1 TO 4 :: READ QX(I),QY(I):: NEXT I :: FOR I=1 TO 4 :: READ SE$(I):: NEXT I
120 RANDOMIZE :: CALL SCREEN(2):: CALL CLEAR :: FOR I=1 TO 14 :: READ C :: CALL COLOR(I,C,1):: NEXT I :: FOR I=1 TO 19 :: READ MS$(I):: NEXT I
125 DISPLAY AT(1,1)SIZE(28):MS$(1):: GOSUB 700 :: FOR I=1 TO 4 :: READ V,W,L$ :: LN(I)=V :: LC(I)=W :: CALL CHAR(V,L$):: NEXT I
130 FOR I=1 TO FS :: CALL HCHAR(I+FY-1,FX,LN(1),FS):: NEXT I :: FOR I=1 TO 5 :: READ CC,FC$ :: CALL CHAR(CC,FC$):: NEXT I
140 CALL HCHAR(FY,FX-1,49):: CALL HCHAR(FY,FX+FS,50):: CALL HCHAR(FY+FS-1,FX-1,51):: CALL HCHAR(FY+FS-1,FX+FS,52)
150 FOR I=1 TO 5 :: DISPLAY AT(FY+((I-1)*2),FX+FS+1)SIZE(8):"(";CHR$(I+64);") ";KA$(I):: NEXT I
200 K=0 :: Q=0 :: AC=0 :: M$=MS$(2):: GOSUB 700 :: GOSUB 800
230 CALL KEY(0,K,S):: IF S=0 THEN 230 ELSE Q=K-64 :: IF Q>0 AND Q<6 THEN ON Q GOTO 240,240,240,250,270 ELSE 230
240 AC=Q :: NL=LN(AC):: NC=LC(AC):: M$=KA$(AC)&MS$(4):: MX$="" :: GOSUB 800 :: GOTO 300
250 IF MN>250 AND FM<5 THEN FM=FM+1 :: MN=MN-250 :: MX$=MS$(5)ELSE IF MN<250 THEN MX$=MS$(6)ELSE MX$=MS$(7)
260 GOSUB 800 :: GOSUB 400 :: GOSUB 700 :: IF YR<2000 THEN 600
270 CALL CLEAR :: CALL SCREEN(8):: PRINT MS$(8);MN :: PRINT MS$(9);SE$(SN);" ";YR :: END
300 CALL KEY(0,K,S):: IF S=0 THEN 300 ELSE Q=K-48 :: IF Q>0 AND Q<5 THEN QD=Q :: SX=QX(QD):: SY=QY(QD):: SC=7 :: GOSUB 500 :: GOTO 260 ELSE 300
400 SN=SN+1 :: IF SN>4 THEN SN=1 :: YR=YR+1 :: M$=MS$(3):: GOSUB 800 :: AC=0 :: NL=LN(4):: NC=LC(4):: SX=FX :: SY=FY :: SC=15 :: GOSUB 500 :: RETURN ELSE RETURN
500 FOR Y=SY TO SY+SC :: FOR X=SX TO SX+SC :: CALL PT(Y,X,NL,NC,AC,MN):: NEXT X :: NEXT Y :: GOSUB 700 :: RETURN
600 CN=INT(RND*2)+1 :: ON INT(RND*20)+1 GOTO 200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,610,620,630,640,650
610 IF CN=1 THEN NL=LN(2):: NC=LN(1):: MX$=MS$(10):: SC=18-(FM*2):: GOTO 666 ELSE NL=LN(1):: NC=LN(2):: SC=8+(FM*2):: MX$=MS$(15):: GOTO 666
620 IF CN=1 THEN NL=LN(4):: NC=LN(2):: MX$=MS$(11):: SC=18-(FM*2):: GOTO 666 ELSE NL=LN(2):: NC=LN(4):: SC=8+(FM*2):: MX$=MS$(16):: GOTO 666
630 IF CN=1 THEN NL=LN(3):: NC=LN(2):: MX$=MS$(12):: SC=18-(FM*2):: GOTO 666 ELSE NL=LN(4):: NC=LN(3):: SC=8+(FM*2):: MX$=MS$(17):: GOTO 666
640 IF CN=1 AND FM>1 THEN FM=FM-1 :: MX$=MS$(13):: GOSUB 800 :: GOTO 200 ELSE IF FM<5 THEN FM=FM+1 :: MX$=MS$(18):: GOSUB 800 :: GOTO 200 ELSE 200
650 IF CN=1 THEN MN=MN-INT(MN/(9+FM)):: MX$=MS$(14):: GOSUB 800 :: GOTO 200 ELSE MN=MN+INT(MN/(11-FM)):: MX$=MS$(19):: GOTO 200
666 GOSUB 700 :: SY=INT(RND*(FS-SC))+FY :: SX=INT(RND*(FS-SC))+FX :: GOSUB 500 :: GOTO 200
700 DISPLAY AT(3,1)SIZE(28):SE$(SN);" ";YR;" $";MN :: DISPLAY AT(5,1)SIZE(28):MX$ :: RETURN
800 DISPLAY AT(24,1)SIZE(28):M$ :: FOR I=1 TO FM :: CALL HCHAR(20,22+I,127+I):: NEXT I :: FOR I=FM+1 TO 5 :: CALL HCHAR(20,22+I,32):: NEXT I :: RETURN
900 DATA "PLOW","SEED","REAP","HIRE","QUIT"
910 DATA 3,7,11,7,3,15,11,15,"SUMMER","AUTUMN","WINTER","SPRING"
915 DATA 15,15,15,15,15,15,15,15,11,12,4,13,10,14
920 DATA "OLD McOWEN'S TI FARM","WHAT WILL YOU DO NEXT?","CROPS ARE GROWING..."," WHICH QUAD? (1-4)"
930 DATA "YOU'VE HIRED A FARMHAND","NOT ENOUGH MONEY","YOU HAVE A FULL STAFF","YOU EARNED $"," BY "
940 DATA "DROUGHT BAKES YOUR SOIL!","BIRDS EAT YOUR SEEDS!","INSECTS RAVAGE YOUR CROPS!","ZOMBIES EAT A FARMHAND!","TAXES COME DUE!"
950 DATA "A NEW TRACTOR!","SELF-SEEDING CROP WORKS!","ABUNDANT RAINS!","VOLUNTEER FARMHAND!","WINDFALL PROFITS THIS YEAR!"
960 DATA 96,104,"BCDBA7B95AE7D73B",104,112,"FF9F66F9FF9F66F9",120,96,"E7DB995A7EBD99A5",112,120,"AAA22A88AAA22A88"
970 DATA 128,"885C487C4A485454",129,"183C3C187E993C7E",130,"1848583C1A1C286C",131,"183C187EBD3C2466",132,"00183A0A3C481422"
1000 SUB PT(Y,X,NL,NC,AC,MN)
1010 CALL GCHAR(Y,X,G):: IF G=NL THEN CALL HCHAR(Y,X,NC):: IF AC=3 THEN MN=MN+1
1020 SUBEND
